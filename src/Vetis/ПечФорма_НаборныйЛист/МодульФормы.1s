
Функция ПоказатьДатуИзготовления(Партия)
    Если Партия.Выбран() = 1 Тогда
        Возврат ГМ.Преобразовать_СтрокаДата_в_ФормализованнаяДата( Партия.ДатаИзготовления1 );
	Иначе
		Возврат "";
    КонецЕсли;
КонецФункции          

Функция ПоказатьГоденДо(Партия)
    Если Партия.Выбран() = 1 Тогда
        Возврат ГМ.Преобразовать_СтрокаДата_в_ФормализованнаяДата( Партия.ДатаСрокГодности1 );
	Иначе
		Возврат "";
    КонецЕсли;
КонецФункции

Процедура ПечатьНаборныйЛист(Докум, Устройство=0, КолвоКопий=0, СразуНаПринтер=0)

	Таб = СоздатьОбъект("Таблица");
	Таб.ИсходнаяТаблица("ФормаТранзакции");
	
	НомерДок =  ГМ.глНомерНаПечать(Докум);
    ОтправительТранзакции =  ГМ.ПолучитьНаименованиеКлиента(Докум.Отправитель_ХозСубъект.Контрагент); 
    ПолучательТранзакции =  ГМ.ПолучитьНаименованиеКлиента(Докум.Получатель_ХозСубъект.Контрагент);
	
	ДокОснование = Докум.ДокОснование;
	
	//Для проверки количества номенклатуры из ВСД в +, из накладной в - 
	ТаблицаПроверки = СоздатьОбъект("ТаблицаЗначений");
	ТаблицаПроверки.НоваяКолонка("Номенклатура");
	ТаблицаПроверки.НоваяКолонка("Количество", "Число", 15, 2);		

	Если (ДокОснование.Выбран() = 1) и (ДокОснование.ПометкаУдаления() = 0) Тогда
		
		ДокВСД2=СоздатьОбъект("Документ");	
		ДокВСД2.ВыбратьПодчиненныеДокументы(,,ДокОснование);
		Пока ДокВСД2.ПолучитьДокумент() = 1 Цикл
			
			Если ( ВРЕГ(ДокВСД2.Вид()) <> "ВСД2_ТРАНЗАКЦИЯ" ) ИЛИ 
				 ( ДокВСД2.ПометкаУдаления() = 1 ) Тогда
				Продолжить;	
			КонецЕсли;
			
			Пока ДокВСД2.ПолучитьСтроку() = 1 Цикл
				ТаблицаПроверки.НоваяСтрока();
				ТаблицаПроверки.Номенклатура = ДокВСД2.Номенклатура;
				ТаблицаПроверки.Количество = ДокВСД2.Количество;
			КонецЦикла;			
		КонецЦикла;
		
		// ДокОснование.Вид() = "Реализация"
		Если Найти( ГМ.ПолучитьКонстанту("НазваниеВидаДокументаРеализация"), ДокОснование.Вид() ) >0 Тогда
			тзДок = СоздатьОбъект("ТаблицаЗначений");
			ДокОснование.ВыгрузитьТабличнуюЧасть(тзДок);
			тзДок.ВыбратьСтроки();
			Пока тзДок.ПолучитьСтроку() = 1 Цикл			
				ТаблицаПроверки.НоваяСтрока();
				ТаблицаПроверки.Номенклатура = тзДок.ПолучитьЗначение( тзДок.НомерСтроки, ГМ.ПолучитьКонстанту("НазваниеРеквизитаНоменклатура") );
				Попытка
					Если тзДок.Единица.Вес>0 Тогда
						ТаблицаПроверки.Количество = тзДок.Количество * тзДок.Единица.Вес;
					Иначе
						ТаблицаПроверки.Количество = тзДок.Количество;
					КонецЕсли;
				Исключение
					ТаблицаПроверки.Количество = тзДок.Количество;
				КонецПопытки;				
				Попытка
					Если ТаблицаПроверки.Номенклатура.БазоваяЕдиница.Коэффициент > 0  Тогда 
						Коэф = ТаблицаПроверки.Номенклатура.БазоваяЕдиница.Коэффициент;
					Иначе
						Коэф = 1;
					КонецЕсли 
				Исключение 
					Коэф = 1;
				КонецПопытки;
				ТаблицаПроверки.Количество = - ТаблицаПроверки.Количество / Коэф;
				
			КонецЦикла;
		Иначе
			СтрокаВнимания = "ВНИМАНИЕ!!! Документ основание неустановленного типа, обратитесь к администратору";
			Таб.ВывестиСекцию("СтрокаВнимания|Осн");
			ТаблицаПроверки.УдалитьСтроки();	
		КонецЕсли;
	Иначе
		СтрокаВнимания = "ВНИМАНИЕ!!! Документ основание не выбран, проверка количества невозможна!";
		Таб.ВывестиСекцию("СтрокаВнимания|Осн");
		ТаблицаПроверки.УдалитьСтроки();
	КонецЕсли;
	
	ТаблицаПроверки.Свернуть("Номенклатура", "Количество");
	
	НужноВыводитьОшибки = 0;
	ТаблицаПроверки.ВыбратьСтроки();
	Пока ТаблицаПроверки.ПолучитьСтроку() = 1 Цикл
	    Если ТаблицаПроверки.Количество <> 0 Тогда
	    	НужноВыводитьОшибки = 1;
			Прервать;
	    КонецЕсли;			
	КонецЦикла;
	
	Таб.ВывестиСекцию("Сек_1|Осн");
	Если НужноВыводитьОшибки = 1 Тогда
		Таб.ПрисоединитьСекцию("Сек_1|Ошибки");	
	КонецЕсли;
	
	Докум.ВыбратьСтроки();
	НомПП = 0;
	
	Пока Докум.ПолучитьСтроку() = 1 Цикл
		НомПП = НомПП + 1;
		
		НоменклатураТранзакции =  СокрЛП(Докум.НаименованиеПродукции);
		ВесПартии = Докум.Количество;
		ДатаИзготовления = ПоказатьДатуИзготовления(Докум.Партия);
		СрокГодности = ПоказатьГоденДо(Докум.Партия);
		НоменклатураВнутренняя = Докум.Номенклатура;
		
		//Проверка номенклатуры
		НайденнаяСтрока = "";
		СтрокаОшибки = "";
		Если ТаблицаПроверки.НайтиЗначение(Докум.Номенклатура, НайденнаяСтрока, "Номенклатура") = 1 Тогда
			ТаблицаПроверки.ПолучитьСтрокуПоНомеру(НайденнаяСтрока);
			Если ТаблицаПроверки.Количество > 0 Тогда
				СтрокаОшибки = "В ВСД указано количество больше чем в накладной на " + ТаблицаПроверки.Количество;
			ИначеЕсли ТаблицаПроверки.Количество < 0 Тогда
				СтрокаОшибки = "В накладной указано количество больше чем в ВСД на " + (-ТаблицаПроверки.Количество);
			Иначе
				СтрокаОшибки = "";
			КонецЕсли;

			ТаблицаПроверки.УдалитьСтроку(НайденнаяСтрока);				
		КонецЕсли;; 
		
		Таб.ВывестиСекцию("Строка|Осн");
		Если НужноВыводитьОшибки = 1 Тогда
			Таб.ПрисоединитьСекцию("Строка|Ошибки");	
		КонецЕсли;
	КонецЦикла;
    
	ТаблицаПроверки.ВыбратьСтроки();
	Пока ТаблицаПроверки.ПолучитьСтроку() = 1 Цикл
		НомПП = НомПП + 1;	
		НоменклатураВнутренняя = ТаблицаПроверки.Номенклатура;
		ВесПартии = -ТаблицаПроверки.Количество;
		Таб.ВывестиСекцию("СтрокаОшибки|Осн");
		СтрокаОшибки = "Данная номенклатура отсутствует в ВСД";		
		Таб.ПрисоединитьСекцию("СтрокаОшибки|Ошибки");		
	КонецЦикла;
	
   	Таб.ПараметрыСтраницы(2,100,1,0,0,0,0,0,0,0,0,Устройство);
   	Таб.ТолькоПросмотр(1);
    таб.Повторятьприпечатистроки(1,3);

	Если СразуНаПринтер = 0 Тогда
		Таб.Опции(0,0,0,0,"НаборныйЛист");//"ОпцииПечатиРеализация"
		Таб.Показать("НаборныйЛист_" + СокрЛП(Докум.НомерДок) );
	Иначе
		//Таб.КоличествоЭкземпляров(КолвоКопий);
		Таб.Напечатать(0);
	КонецЕсли;

КонецПроцедуры

//******************************************************************************
// ПоКнопкеПечать()
//
// Параметры:
//  Нет
//
// Возвращаемое значение:
//  Нет
//
// Описание:
//
Процедура ПоКнопкеПечать()

	Если Док.Выбран() = 0 Тогда
	    Предупреждение("Не выбран документ!", 60);
		Возврат;
	КонецЕсли;

	ПечатьНаборныйЛист(Док);

КонецПроцедуры // ПоКнопкеПечать()

//******************************************************************************
// Предопределенная процедура
//
Процедура ПриОткрытии()
	
	Устройство = 0;
	КолвоКопий = 0;
	СразуНаПринтер = 0;
	Если ПустоеЗначение(Форма.Параметр) = 0 Тогда
		Если ТипЗначенияСтр(Форма.Параметр) = "СписокЗначений" Тогда
			Докум      = Форма.Параметр.Получить("Контекст");
			Устройство = Форма.Параметр.Получить("Устройство");
			КолвоКопий = Форма.Параметр.Получить("КоличествоКопий");
			СразуНаПринтер = Число(Форма.Параметр.Получить("СразуПечать"));
		ИначеЕсли ТипЗначенияСтр(Форма.Параметр) = "Документ" Тогда
			Докум = Форма.Параметр;
		КонецЕсли;
		ПечатьНаборныйЛист(Докум, Устройство, КолвоКопий, СразуНаПринтер);
		Статусвозврата(0);
		Возврат;
	КонецЕсли;

КонецПроцедуры // ПриОткрытии()
