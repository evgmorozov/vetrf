////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ
// 
Перем ТаблПоле, ТаблицаПартий;
Перем оПривязки; //:Меркурий.Привязки
Перем ВыбФирма Экспорт;

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//                          

//******************************************************************************
Процедура ЗаполнитьСписокПартий()  
	ТаблицаПартий.УдалитьСтроки();   
	
	Запрос=СоздатьОбъект("Запрос"); 
	Текст=	"//{{ЗАПРОС(Сформировать)
	|ВСД_Партия = Справочник.ВСД_Партия.ТекущийЭлемент;
	|ВсдДата = Справочник.ВСД_Партия.ВсдДата;
	|Площадка = Справочник.ВСД_Партия.Получатель_Площадка; 
	|ДатаИзготовления1 = Справочник.ВСД_Партия.ДатаИзготовления1;
	|Продукция_Элемент = Справочник.ВСД_Партия.Продукция_Элемент;
	|Группировка ВСД_Партия без групп;
	|Условие (ВсдДата=ДатаСоздания);
	|Условие (Площадка = Отправитель_Площадка);
	|"//}}ЗАПРОС
    ;        
	Если ФлФильтрВыработка=1 Тогда
		Текст=Текст+"Условие(ДатаИзготовления1=ДатаВыработки);";
	КонецЕсли;	    
	Если ВыбПродукция.Выбран()=1 Тогда
		Текст=Текст+"Условие(Продукция_Элемент=ВыбПродукция);";
	КонецЕсли;	    
	СписокПартий=СоздатьОбъект("СписокЗначений");
	Запрос.Выполнить(Текст); 
	Пока Запрос.Группировка(1)=1 Цикл
		ТаблицаПартий.НоваяСтрока();
		ТаблицаПартий.НомерЗаписи=сокрлп(Запрос.ВСД_Партия.НомерЗаписи);  
		ТаблицаПартий.Партия=Запрос.ВСД_Партия;  
		ТаблицаПартий.Выработка=Запрос.ВСД_Партия.ДатаИзготовления1;  
		ТаблицаПартий.Отправка=Запрос.ВСД_Партия.ВсдДата;   
		ТаблицаПартий.Продукция=Запрос.ВСД_Партия.Продукция_Элемент;   
		ТаблицаПартий.Количество=Запрос.ВСД_Партия.Количество;   
		ТаблицаПартий.Статус=Запрос.ВСД_Партия.Статус;    
		СписокПартий.ДобавитьЗначение(Запрос.ВСД_Партия);
	КонецЦикла;	     
	
	Если ДатаСоздания > РабочаяДата() Тогда
		ДатаСоздания2 = ДатаСоздания;
	Иначе
		ДатаСоздания2 = РабочаяДата();
	КонецЕсли;
	
	Запрос=СоздатьОбъект("Запрос"); 
	Текст=	"//{{ЗАПРОС(Партии)
	|Период с (ДатаСоздания) по (ДатаСоздания2);
	|ОбрабатыватьДокументы все;
	|Обрабатывать НеПомеченныеНаУдаление;
	|Док = Документ.ВСД2_ЛабораторныеИсследования.ТекущийДокумент;
	|Партия = Документ.ВСД2_ЛабораторныеИсследования.Партия;
	|Группировка Партия без групп;
	|Группировка Док;
	|Условие(Партия в СписокПартий);
	|"//}}ЗАПРОС
    ;
	Если Запрос.Выполнить(Текст)>0 Тогда
		ТаблицаПартий.ВыбратьСтроки();
		Пока ТаблицаПартий.ПолучитьСтроку()=1 Цикл
			СписокДокументовЛИ=СоздатьОбъект("СписокЗначений");
			Если Запрос.Получить(ТаблицаПартий.Партия,)>0 Тогда
				Пока Запрос.Группировка(2)=1 Цикл
					СписокДокументовЛИ.ДобавитьЗначение(Запрос.Док);
				КонецЦикла;	
				Запрос.вНачалоВыборки();
			КонецЕсли;	
			ТаблицаПартий.СписокДокументовЛИ=СписокДокументовЛИ; 
			Если СписокДокументовЛИ.РазмерСписка()>0 Тогда
				ТаблицаПартий.ФлЛИЕ=1;
			КонецЕсли;	
			Для й=1 по СписокДокументовЛИ.РазмерСписка() Цикл
				Док=СписокДокументовЛИ.ПолучитьЗначение(й);
				Если сокрлп(Док.Статус)="COMPLETED" Тогда
					ТаблицаПартий.ФлЛИО=1;
					Прервать;
				КонецЕсли;	
			КонецЦикла;	
		КонецЦикла;	
	КонецЕсли;	
	 
	ТаблПоле.ОбновитьСтроки();
	//Возврат;
	//
	//ЗапросSQL=СоздатьОбъект("ODBCRecordSet");  
	//
	//МетаДата=СоздатьОбъект("MetaDataWork");  
	//ИмяСпр="sc"+МетаДата.ИДСправочника("ВСД_Партия");    
	//ИмяСпрДатаОтпр="sp"+МетаДата.ИДРеквизитаСправочника("ВСД_Партия","ВсдДата");      
	//ИмяСпрДатаВыр="sp"+МетаДата.ИДРеквизитаСправочника("ВСД_Партия","ДатаИзготовления1");   
	//ИмяСпрПродукция="sp"+МетаДата.ИДРеквизитаСправочника("ВСД_Партия","Продукция_Элемент");  
	//
	//ФильтрДатаС1="'"+Лев(формат(ВыбНачПериодаС,"ДДДММГГ"),8)+"'";  
	//ФильтрДатаС2="'"+Лев(формат(ВыбКонПериодаС,"ДДДММГГ"),8)+"'";  
	//          
	//
	//Текст="select id from "+ИмяСпр+" where (where ("+ИмяСпр+"."+ИмяСпрДатаОтпр+">="+ФильтрДатаС1+") "+
	//"and "+ "("+ИмяСпр+"."+ИмяСпрДатаОтпр+"<="+ФильтрДатаС2+") ";  
	//
	////Текст="select * from "+ИмяСпр; 
	//
	//Если ЗапросSQL.Открыть(Текст)=1 Тогда
	//	ТЗ=СоздатьОбъект("ТаблицаЗначений");
	//	ЗапросSQL.ПолучитьРезультатыВ_ТЗ(ТЗ,1);   
	//	//ТЗ.ВыбратьСтроку();
	//	//Возврат;
	//	
	//	ТЗ.ВыбратьСтроки();
	//	Пока ТЗ.ПолучитьСтроку()=1 Цикл   
	//		Спр=МетаДата.ЗначениеИзСтрокиБД("Справочник.ВСД_Партия",ТЗ.id); 
	//		ТаблицаПартий.НоваяСтрока();
	//	КонецЦикла;	                   
	//КонецЕсли;	
КонецПроцедуры	 

//******************************************************************************
Процедура ОбнулитьПродукцию()
	ВыбПродукция = 0;	
	ЗаполнитьСписокПартий();
КонецПроцедуры // ОбнулитьПродукцию

//******************************************************************************
Процедура ТаблПолеПриАктивизацииСтроки(ТабличноеПоле)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, ТаблицаПартий);
КонецПроцедуры

//******************************************************************************
Процедура ТаблПолеПриВыводеСтроки(ТабличноеПоле,ОформлениеСтроки,ДанныеСтроки,ТипРегиона)
	Если ТипРегиона = 3 Тогда
		ГМ.ВывестиФлажок(ОформлениеСтроки, ДанныеСтроки, "Пометка");
		Если СокрЛП(ДанныеСтроки.Статус) = "202" Тогда
			ОформлениеСтроки.ЦветФона= 8421631;		// Красный
		ИначеЕсли ДанныеСтроки.ФлЛИО = 1 Тогда
			ОформлениеСтроки.ЦветФона= 7405253;// 32768;		// Зеленый
		ИначеЕсли ДанныеСтроки.ФлЛИЕ = 1 Тогда
			ОформлениеСтроки.ЦветФона= 8454143;		// Желтый   
		КонецЕсли;
		ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("Партия");
		ОбъектЯчейка.УстановитьТекст( СокрЛП( ДанныеСтроки.Партия.Код )+" / "+СокрЛП(ДанныеСтроки.Партия.НомерПартии) );

		ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("Продукция");
		ОбъектЯчейка.УстановитьТекст( ДанныеСтроки.Продукция.Код+" / "+СокрЛП(ДанныеСтроки.Продукция.Наименование) );		
		
		ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("ЛабИсследования"); 
		
		ТекстЛИ = "";
		Для СЦ = 1 По ДанныеСтроки.СписокДокументовЛИ.РазмерСписка() Цикл
			ТекстЛИ = ТекстЛИ + ДанныеСтроки.СписокДокументовЛИ.ПолучитьЗначение(СЦ).НомерДок+",";
		КонецЦикла;                                                                  
		ТекстЛИ = Лев(ТекстЛИ,СтрДлина(ТекстЛИ)-1);
		ОбъектЯчейка.УстановитьТекст(ТекстЛИ);
		
		Если ДанныеСтроки.ФлЛИЕ = 1 Тогда
			ОбъектЯчейка.УстановитьФлажок(ДанныеСтроки.ФлЛИО);
		КонецЕсли;

		ОбъектЯчейка=ОформлениеСтроки.Ячейки.Получить("Выработка");
		ОбъектЯчейка.УстановитьТекст(ГМ.Преобразовать_СтрокаДата_в_ФормализованнаяДата( ДанныеСтроки.Выработка));
	КонецЕсли;
КонецПроцедуры	 

//******************************************************************************
Процедура ТаблПолеПриВыбореФлажка(ТабличноеПоле,Стр, Колонка, ТипРегиона)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, ТаблицаПартий);     

	Если СокрЛП(ТаблицаПартий.Статус) <> "202" Тогда
		ТаблицаПартий.Пометка = ?(ТаблицаПартий.Пометка = 1,0,1);
		ТабличноеПоле.ОбновитьСтроки();
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура ТаблПолеВыбор(ТабличноеПоле, __стр, Колонка, ТипРегиона)
	ГМ.ПриАктивизацииСтрокиТП(ТабличноеПоле, ТаблицаПартий);
	Если ТипРегиона =3 Тогда
		КодКолонки = Колонка.Данные;
		Если КодКолонки = "Пометка" Тогда
			ТаблицаПартий.Пометка = ?(ТаблицаПартий.Пометка = 1,0,1);
		ИначеЕсли КодКолонки = "Продукция" Тогда
			ОткрытьФорму(ТаблицаПартий.Продукция);
		ИначеЕсли КодКолонки = "ЛабИсследования" Тогда
			СписокДокументовЛИ=ТаблицаПартий.СписокДокументовЛИ;
			Если СписокДокументовЛИ.РазмерСписка()=1 Тогда
				ОткрытьФорму(СписокДокументовЛИ.ПолучитьЗначение(1));
			ИначеЕсли СписокДокументовЛИ.РазмерСписка()>1 Тогда   
				Док=0;
				Если СписокДокументовЛИ.ВыбратьЗначение(Док,,,,0)>0 Тогда  
					ОткрытьФорму(Док);
				КонецЕсли;	
			КонецЕсли;
		Иначе //Если КодКолонки = "Партия" Тогда
			ОткрытьФорму(ТаблицаПартий.Партия);			
		КонецЕсли;
		ТабличноеПоле.ОбновитьСтроки();
	КонецЕсли;
КонецПроцедуры

//******************************************************************************
Процедура ПоКнопкеЗаполнить() 
	ЗаполнитьСписокПартий();
КонецПроцедуры

//******************************************************************************
Процедура ВвестиЛИ()
	ГМ.ПриАктивизацииСтрокиТП(ТаблПоле, ТаблицаПартий);  
	ФирмаИмяРеквизита = "";
	ГМ.ПолучитьИмяРеквизитаФирма( "ВСД2_ЛабораторныеИсследования", ФирмаИмяРеквизита);
	
	Если ПустоеЗначение(ВыбШаблон) = 1 Тогда
		Предупреждение("Выберите документ-шаблон лаб.исследований!");
	ИначеЕсли ТаблицаПартий.Итог("Пометка") = 0 Тогда                    
		Предупреждение("Нет отмеченных партий!");
	Иначе
		ДокНовый = СоздатьОбъект("Документ.ВСД2_ЛабораторныеИсследования"); 
		ТаблицаПартий.ВыбратьСтроки();
		Пока ТаблицаПартий.ПолучитьСтроку() = 1 Цикл
			Если ТаблицаПартий.Пометка = 1 Тогда     
				Если ТаблицаПартий.ФлЛИЕ=0 Тогда
					ДокНовый.Новый();					
					ДокНовый.НомерАктаОтбораПроб=СокрЛП(ВыбШаблон.НомерАктаОтбораПроб);   
					ДокНовый.НаименованиеЛаборатории=СокрЛП(ВыбШаблон.НаименованиеЛаборатории);   
					ДокНовый.НаименованиеПоказателя=СокрЛП(ВыбШаблон.НаименованиеПоказателя);   
					ДокНовый.МетодИсследования=СокрЛП(ВыбШаблон.МетодИсследования);   
					ДокНовый.НомерЭкспертизы=СокрЛП(ВыбШаблон.НомерЭкспертизы);   
					ДокНовый.Заключение=СокрЛП(ВыбШаблон.Заключение);  
					ДокНовый.ДатаОтбораПроб=ВыбШаблон.ДатаОтбораПроб;     
					ДокНовый.ДатаРезультата=ВыбШаблон.ДатаРезультата;     
					ДокНовый.РезультатИсследования=ВыбШаблон.РезультатИсследования;     
					ДокНовый.Партия=ТаблицаПартий.Партия;    
					
					ДокНовый.Отправитель_ХозСубъект = ГМ.СписокКонстант.Получить("Отправитель_ХозСубъект");
					ДокНовый.Отправитель_Площадка = Отправитель_Площадка;
					Если ФирмаИмяРеквизита <> "" Тогда
						ДокНовый.УстановитьАтрибут(ФирмаИмяРеквизита, ВыбФирма);
					КонецЕсли;						
					ДокНовый.Записать();       
					
					ТаблицаПартий.ФлЛИЕ=1;
	
					СписокДокументовЛИ=ТаблицаПартий.СписокДокументовЛИ;
					СписокДокументовЛИ.ДобавитьЗначение(ДокНовый.ТекущийДокумент());
	
					ТаблицаПартий.СписокДокументовЛИ=СписокДокументовЛИ;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;

		ТаблПоле.ОбновитьСтроки(); 
	КонецЕсли;	
КонецПроцедуры 

//******************************************************************************
Процедура ВвестиЛИСписком() // SAA
	
	ДокЛИ=СоздатьОбъект("Документ.ВСД2_ЛабораторныеИсследования");
	ВыбДатаШаблонов=ВосстановитьЗначение("ДатаШаблоновЛабИссл");
	Если ВвестиДату(ВыбДатаШаблонов,"Укажите дату результатов исследований",60)=1 Тогда
		СохранитьЗначение("ДатаШаблоновЛабИссл",ВыбДатаШаблонов);
	Иначе
		Возврат;
	КонецЕсли;
	ДокЛИ.ВыбратьДокументы(ВыбДатаШаблонов,ВыбДатаШаблонов);
	ТекСтрокаПартий=ТаблицаПартий.ТекущаяСтрока();
	Пока ДокЛИ.ПолучитьДокумент() = 1 Цикл
		Если ДокЛИ.ЭтоШаблон=0 Тогда
			Продолжить;
		КонецЕсли;   
		Если ДокЛИ.Продукция_Элемент<>ТаблицаПартий.Продукция Тогда
			Продолжить;
		КонецЕсли;   
		Сообщить("Найден шаблон "+ДокЛИ.Продукция_Элемент+" - "+ДокЛИ.НаименованиеПоказателя);
		ВыбШаблон=ДокЛИ.ТекущийДокумент();
		ВвестиЛИ();
		ТаблицаПартий.ПолучитьСтрокуПоНомеру(ТекСтрокаПартий);
		ТаблицаПартий.ФлЛИЕ=0;
	КонецЦикла;

КонецПроцедуры

//******************************************************************************

Процедура ОтправитьЛабИссл() 
	ТаблицаПартий.ВыбратьСтроки();
	Пока ТаблицаПартий.ПолучитьСтроку()=1 Цикл
		Если ТаблицаПартий.Пометка=1 Тогда    
			Если ТаблицаПартий.ФлЛИЕ = 1 Тогда
				Для СЦ = 1 По ТаблицаПартий.СписокДокументовЛИ.РазмерСписка() Цикл
					ТекДок = ТаблицаПартий.СписокДокументовЛИ.ПолучитьЗначение(СЦ); 
					
					ГМ2.Отправить_ВСД_ЛабораторныеИсследования(ТекДок);    
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;	
	КонецЦикла;	 
	ЗаполнитьСписокПартий();
КонецПроцедуры	

//******************************************************************************
// ГрупповаяПометка(Режим)
//
// Параметры: 
//  Режим:
//   1 - пометить все
//   2 - снять пометку
//   3 - инвертировать пометку
//
// Возвращаемое значение:
//  Нет
//
// Вызывается из формул элементов диалога
// кнопки групповой пометки
//
// Описание:
//  Производит групповые действия с пометкой строк таблицы формы
//
Процедура ГрупповаяПометка(Режим)

	ТаблицаПартий.ВыбратьСтроки();
	Пока ТаблицаПартий.ПолучитьСтроку()=1 Цикл  
		Если СокрЛП(ТаблицаПартий.Статус) <> "202" Тогда
			Если Режим=1 Тогда              
				ТаблицаПартий.Пометка = 1;
			ИначеЕсли Режим=2 Тогда
				ТаблицаПартий.Пометка = 0;
			ИначеЕсли Режим=3 Тогда
				ТаблицаПартий.Пометка =	?(ТаблицаПартий.Пометка=0,1,0);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	ТаблПоле.ОбновитьСтроки();

КонецПроцедуры // ГрупповаяПометка()         

Процедура ПриИзмененииФирмы()
	ГМ.Инициализация(Контекст);
	ГМ.ЗагрузитьПараметрыВФорму(Контекст);
	
	ЗаполнитьСписокПартий();
КонецПроцедуры

//======================================================================
Процедура ПриИзмененииСФ()
	Если СписокФирм.ТекущаяСтрока() <> 0 Тогда
		ВыбФирма = СписокФирм.ПолучитьЗначение(СписокФирм.ТекущаяСтрока());
		ПриИзмененииФирмы();
	КонецЕсли;
КонецПроцедуры // ПриИзмененииСФ



//******************************************************************************
// предопределенная процедура
//
Процедура ПриОткрытии()    
	
	ВыбФирма = "";
	ГМ._ПриОткрытии(Контекст);
	
	ДатаСоздания=РабочаяДата();   
	ДатаВыработки=РабочаяДата();

	ВыбШаблон = ВосстановитьЗначение("ВыбШаблон");
	
	оПривязки=СоздатьОбъект("Меркурий.Привязки");
	оПривязки.УстановитьФорму(Форма);

	Если Форма.МодальныйРежим() = 0 Тогда
	    оПривязки.Привязка("кнЗакрыть", "T", "Форма");
		оПривязки.Привязка("ТаблПоле", "H", "Форма", "W", "Форма");
		оПривязки.Привязка("Версия","T","Форма", "L", "Форма");
	КонецЕсли;	      
КонецПроцедуры

//******************************************************************************
Процедура ПослеСозданияФормы()
	ТаблПоле=ГМ.СоздатьТабличноеПоле(Контекст, "ТаблПоле", ТаблицаПартий, 1, 1);
	
	ЗаполнитьСписокПартий();
КонецПроцедуры      

//{========================= Список изменений =================

Процедура ПриНажатииЛевойКнопки(Сост, х, у)
	
	ФормаРасш = СоздатьОбъект("РасширениеФормы");
	Атр = ФормаРасш.ПолучитьАтрибутПоКоординатам(х,у);
	Если ТипЗначенияСтр(Атр) = "АтрибутФормы" Тогда
		Если Атр.Идентификатор = "Версия" Тогда
			ЗапуститьПриложение(ГМ.ВерсияСсылка());
		КонецЕсли;  
	КонецЕсли;	
	
КонецПроцедуры


//}===========================================================

//{==================== Привязки ================================
//******************************************************************************
Процедура ПослеОткрытия()
	ГМ._ПослеОткрытия(Контекст);
КонецПроцедуры

//******************************************************************************
Процедура ПриИзмененииРазмераОкна(ТипСобытия, НовШирина, НовВысота) Экспорт
	ГМ._ПриИзмененииРазмераОкна(Контекст, ТипСобытия, НовШирина, НовВысота);
КонецПроцедуры
//}===========================================================

//******************************************************************************
Процедура ПриЗакрытии()
	СохранитьЗначение("ВыбШаблон", ВыбШаблон);
КонецПроцедуры // ПриЗакрытии

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 
ТаблицаПартий = СоздатьОбъект("ТаблицаЗначений");
ТаблицаПартий.НоваяКолонка("Пометка","Число",1,,"V",3,,);
ТаблицаПартий.НоваяКолонка("Продукция",,,,,20,,); 
ТаблицаПартий.НоваяКолонка("Выработка",,,,,10,,);
ТаблицаПартий.НоваяКолонка("Отправка",,,,,10,,);
ТаблицаПартий.НоваяКолонка("Количество","Число",12,3,,10,"Ч010.1",2); 
ТаблицаПартий.НоваяКолонка("Партия",,,,,6,,); 
ТаблицаПартий.НоваяКолонка("НомерЗаписи",,,,"№ записи",8,,);  
ТаблицаПартий.НоваяКолонка("Статус",,,,,3,,);   
ТаблицаПартий.НоваяКолонка("ЛабИсследования",,,,"Лаб.исследования",10,,);
ТаблицаПартий.НоваяКолонка("ФлЛИЕ","Число",1,,"ЛИ есть",6,,);
ТаблицаПартий.НоваяКолонка("ФлЛИО","Число",1,,"ЛИ отпр",6,,);
ТаблицаПартий.НоваяКолонка("СписокДокументовЛИ",,,,,6,,); 
ТаблицаПартий.ВидимостьКолонки("СписокДокументовЛИ",0); 
ТаблицаПартий.ВидимостьКолонки("ФлЛИЕ",0); 
ТаблицаПартий.ВидимостьКолонки("ФлЛИО",0); 
